(cl:in-package :pz-godot)

(declaim (special *exports*))

(defparameter *extension-api-file* (asdf:system-relative-pathname :pz-godot/wrapper "src/api/extension_api.json"))

(defparameter *gdextension-interface-file* (asdf:system-relative-pathname :pz-godot/wrapper "src/api/gdextension_interface.json"))

(defparameter *gdextension-type-bindings-file* (asdf:system-relative-pathname :pz-godot/wrapper "bindings/gdext-types.lisp"))

(defparameter *gdextension-interface-bindings-file* (asdf:system-relative-pathname :pz-godot/wrapper "bindings/gdext-interface.lisp"))

(defparameter *type-string-regex* (ppcre:create-scanner "(const)?\\s*(\\w+)\\s*(\\W+)?"))

(defparameter *camel-case-splitter* (ppcre:create-scanner "([A-Z]+[^A-Z]*)+?"))

(defparameter *constant-splitter* (ppcre:create-scanner "([A-Z]+[^A-Z]*)+?"))



(defun keywordify (name)
  (a:make-keyword (uiop:standard-case-symbol-name name)))


(defun symbolicate-gdext-camel-case (name &key (package *package*) (skip-first t))
  (let ((parts (ppcre:all-matches-as-strings *camel-case-splitter* name)))
    (a:format-symbol package "窿蕲"
                     (mapcar #'uiop:standard-case-symbol-name
                             (if skip-first
                                 (rest parts)
                                 parts)))))


(defun symbolicate-gdext-snake-case (name &key (package *package*) (skip-first t))
  (let ((parts (ppcre:split "_" name)))
    (a:format-symbol package "窿蕲"
                     (mapcar #'uiop:standard-case-symbol-name
                             (if skip-first
                                 (rest parts)
                                 parts)))))


(defun parse-type-string (type-string &key (package *package*))
  (multiple-value-bind (match groups)
      (ppcre:scan-to-strings *type-string-regex* type-string)
    (declare (ignore match))
    (let ((type (let ((type-name (aref groups 1)))
                  (if (a:starts-with-subseq "GDExtension" type-name)
                      (symbolicate-gdext-camel-case type-name :package package)
                      (a:eswitch (type-name :test #'equal)
                        ("int8_t" :int8)
                        ("uint8_t" :uint8)
                        ("int16_t" :int16)
                        ("uint16_t" :uint16)
                        ("int32_t" :int32)
                        ("uint32_t" :uint32)
                        ("int64_t" :int64)
                        ("uint64_t" :uint64)
                        ("float" :float)
                        ("double" :double)
                        ("char" :char)
                        ("char16_t" :uint16)
                        ("char32_t" :uint32)
                        ("size_t" :size)
                        ("void" :void)

                        ("wchar_t" '%gdext.common:wchar)
                        ("GDObjectInstanceID"
                         (symbolicate-gdext-camel-case type-name :package package)))))))
      (a:eswitch ((aref groups 2) :test #'equal)
        (nil type)
        ("*" `(:pointer ,type))
        ("**" `(:pointer (:pointer ,type)))))))


(defun expand-gdext-documentation (description deprecated)
  (when (or description deprecated)
    (let ((deprecation-text (when deprecated
                              (let ((since (gethash "since" deprecated)))
                                (a:if-let ((replaced-with (gethash "replaced_with" deprecated)))
                                  (format nil "REPLACED with A since A" replaced-with since)
                                  (format nil "DEPRECATED since A" since))))))
      (list (format nil "@[A]@[窿茛溴痱邈狒轱瞽翦ㄣ镥蜚溴筱蜷痿轱ъ轶舂┅┅ㄤ彐珏铄蜷屮痨镤瀛玟屮舡豉疱秕腴钿脲犰祜鳝雉桢颦脲螬ㄤ彐礤翳镤屮痨镤瀛玟屮舡豉疱衡彐矧秕腴钿脲ㄦ矧磲秕ア┅ㄤ彐礤翳镤屮痨镤瀛玟屮舡豉疱秕腴钿ㄥ耢横扉狍┅脲钺礤溴筱蜷痿轱豉疱溴痱邈狒邃戾è钺礤簌簌礅镬殂狒瀛玟屮舡汜礤飙汜箦钺礤┅瘐箬钺礤簌屮痫螋螵痱轭啜沔骈轰彐泗疱簌礅镬殂狒瀛玟屮舡汜礤飙汜箦钺礤疳蝮瀛豉疱篝蜷铉豉疱括屮疳钿玟屮舡滹沲礤铘狒轱溴筱蜷痿轱溴痱邈狒邃┅秕舂┅ㄤ彐礤翳镤屮痨镤瀛玟屮舡豉疱秕腴钿ㄥ耢鸿犷潇濠脲钺礤溴筱蜷痿轱疳蝈铘è洪筮泔铙泔铙舡皓è洪筮躅轭轸獒扉邃躅轭轸獒扉邃皓溴痱邈狒邃ㄤ邈灬蝈ㄩ珙矧泔铙舡躅轭轸獒扉邃疳蝈铘┅戾è钺礤簌簌礅镬殂狒瀛玟屮舡汜礤飙汜箦钺礤┅瘐箬钺礤簌屮痫螋螵痱轭啜沔骈轰彐泗疱钺礤簌ê痫轭翦忽镩洎括屮疳钿玟屮舡滹沲礤铘狒轱溴筱蜷痿轱溴痱邈狒邃┅秕舂┅ㄤ彐礤翳镤屮痨镤瀛玟屮舡豉疱秕腴钿ㄥ耢哄铛愆脲钺礤溴筱蜷痿轱è洪筮忾翩殄熹忾翩殄熹皓鲠祯弩溴痱邈狒邃戾è钺礤簌簌礅镬殂狒瀛玟屮舡汜礤飙汜箦钺礤┅ㄣ镯盹瞽痱彐轼殇ㄩ冀戾铉翳鲠祯弩暴祜镳鏖翳骈蝮ㄧ弭栳箬㈩犴澧ㄡ蝈鲠祯弩癌骘鲠祯徙蝻篌鲠祯弩骘雉桢ㄧ弭栳箬㈩犴澧鲠祯濠躅戾篌篝蜷铉骈蝮雉桢颟黹铋黹黹箜狒汨骈蝮雉桢颟┅┅瘐箬钺礤簌屮痫螋螵痱轭啜ㄩ忾翩殄熹с骀楹溴驸轸骈屐с骀楹溴驺孱蹴钺礤簌括屮疳钿玟屮舡滹沲礤铘狒轱溴筱蜷痿轱溴痱邈狒邃括祜镳骘鲠祯徙蝻篌鲠祯弩骘钺礤篚怏羼ㄧ弭栳箬㈩犴澧鲠祯濠泔眄镱痱彐轼殇泔祆邈啜脲黠蜾殒簌礅镬殂狒瀛玟屮舡箢犭瀛汜箦钺礤后腴瓠骈蝮铋飑ㄧ弭栳箬Ⅵ犰蹂鲠祯濠┅秕舂┅ㄤ彐礤翳镤屮痨镤瀛玟屮舡豉疱秕腴钿ㄥ耢烘躅泗轱瞟脲钺礤溴筱蜷痿轱狎珲礤铘è候弭躜钸鲠祯蝈趱蝾豉疱┅溴痱邈狒邃戾è钺礤簌簌礅镬殂狒瀛玟屮舡汜礤飙汜箦钺礤┅瘐箬钺礤簌屮痫螋螵痱轭啜沔骈轰彐泗疱钺礤簌ê痫轭翦忽镩洎括屮疳钿玟屮舡滹沲礤铘狒轱溴筱蜷痿轱溴痱邈狒邃┅秕舂ㄦ矧磲秕ア戾è沔骈蝈趱蝾豉疱ㄩ蝈趱蝾豉疱疳蝮瀛豉疱篝蜷铉ㄧ弭栳箬Ⅳ疱蝈趱蝾豉疱┅忽镩洎ㄣ骀榄疳蜥憝豉疱麒孱狎珲礤铘祜镳骘狎绛溴徙蝻篌狎珲礤铘泔祆邈疳蝮瀛豉疱篝蜷铉ㄧ弭栳箬Ⅳ疱狎绛溴姗┅┅痱轭啜ョ溴舢泔眄镱轰彐沔躅痱雉钺礤簌沔骈蝈趱蝾豉疱楞骀榄疳蜥憝豉疱螬秕舂┅ㄤ彐礤翳镤屮痨镤瀛玟屮舡豉疱秕腴钿ㄥ耢后趄蹉舂脲钺礤溴筱蜷痿轱礤礅弪溴痱邈狒邃戾è钺礤簌簌礅镬殂狒瀛玟屮舡汜礤飙汜箦钺礤┅瘐箬钺礤簌屮痫螋螵痱轭啜沔骈轰彐泱趄蹉钺礤簌括屮疳钿玟屮舡滹沲礤铘狒轱溴筱蜷痿轱溴痱邈狒邃括祜镳骘礤礅弪徙蝻篌礤礅弪泔祆邈啜簌礅镬殂狒瀛玟屮舡箢犭瀛汜箦ㄧ弭栳箬㈩犴澧礤礅弪后腴瓠骈蝮铋飑疳蝮瀛豉疱篝蜷铉ㄧ弭栳箬Ⅳ疱礤礅弪┅┅秕舂翦蝠蜷秕舂痱轭啜沔骈轰彐泗疱钺礤簌ê篝蝓泗钺礤簌愆秕舂┅ㄤ彐躅屮痨镤瀛玟屮舡豉疱秕豉疱螬祜镳骘豉疱徙蝻篌豉疱滹眭祠轲戾鲠祯瀛忾钿腴钿蝈篝溴姗祜镳鏖翳腴钿铋骘脲忮轭翳栳箬脲轭豉疱躞轭ㄨ狍璀鲠祯鲠祯濠骘脲簌脲黠蜾殒脲殒ㄥ弘轭脲簌愆滹箦翩腴钿脲黠蜾殒鲠祯濠屐箦狃疱钿扉篝脲簌鲠祯濠轭麸蝈篝溴骈钺祆蝈趱蝾鲠祯弩腴钿蝈篝溴姗┅ㄡ痧禊＇屮痨镤瀛玟屮舡豉疱秕腴钿蝈篝溴姗┅ㄤ彐躅珏铄蜥翦玟屮舡豉疱忾钿轭珞豉疱螬ㄡ瑚轸璀秕麴豸麸骈戾秕玟屮翦铙轱瞽豉疱忾钿轭珞骈戾洪姝屮轶趔后躔弪箦溴戾è痱轭舡汜箦轰秣钽狍濠í痱轭舡痱弭豉舂í疳汶徵濯ㄦ轭洵疳汶徵吼顼滹舡痱轶糸铄┅í屮痫螋螵┅痱轭Ж蹰镳轰彐轭瀛疳汶徵亥玟屮舢豉疱ê躞濠秕舂翦蝠蜷秕舂痱轭Ж沆洪瞽疳汶徵亥玟屮舢豉疱螬秕舂ㄥ痨镤瀛玟屮舡豉疱秕豉疱螬翦蝠蜷秕舂翦蝠蜷秕舂痱轭啜沆哄痫螋Ж括铗弼弪箦屮痫螋螵┅秕舂┅ㄤ彐躅屮痨镤瀛玟屮舡轭翦蜴徙瀛骢钽糸镱秕骢钽糸镱溴姗戾舄è骢钽糸镱钺礤ㄧ弭栳箬㈩犴澧骢钽糸镱溴姗钺礤簌簌礅镬殂狒瀛玟屮舡箢犭瀛汜箦骢钽糸镱钺礤后腴瓠骈蝮铋飑ㄡ蜱蹴孱趔祜镳骘狎绛溴徙蝻篌ㄧ弭栳箬⑨蜱蹴孱趔骢钽糸镱溴姗骘钺礤簌礅镬殂狒瀛玟屮舡箢犭瀛汜箦ㄧ弭栳箬㈩犴澧狎绛溴姗后腴瓠骈蝮铋飑骘豉疱疳蝮瀛豉疱篝蜷铉ㄧ弭栳箬Ⅳ疱狎绛溴姗吼徙脶珏ㄦ轭洵疳汶徵亥玟屮舢豉疱螬泔祆邈扉篝钺礤豉疱┅┅瘐箬钺礤簌屮痫螋螵痱轭啜ョ溴舢泔眄镱轰彐殒躅ì骢钽糸镱钺礤钺礤簌愆ㄡ洪姝戾è蝈趱蝾豉疱ㄧ弭栳箬Ⅱ弭躜钸鲠祯澧骢钽糸镱溴姗┅疳蝮瀛豉疱篝蜷铉ㄧ弭栳箬Ⅳ疱蝈趱蝾豉疱吼徙脶珏ㄦ轭洵疳汶徵亥玟屮舢豉疱螬忽镩洎泪蜱蹴孱趔秕舂┅ㄤ彐躅屮痨镤瀛玟屮舡轭翦蜴徙秕轭翦蜴徙濠祜镳骘骢钽糸镱徙蝻篌轭翦蜴徙滹ㄦ矧磲秕アㄥ痨镤瀛玟屮舡轭翦蜴徙瀛骢钽糸镱秕骢钽糸镱┅ㄤ彐躅珏铄蜥翦玟屮舡轭翦蜴徙瀛忾钿轭珞ㄩ铘弪驷沐蹰镳哄铙躜瀛疳汶徵亥玟屮舢豉疱螬ㄡ瑚轸璀秕麴豸麸骈戾秕玟屮翦铙轱瞽轭翦蜴徙瀛忾钿轭珞骈戾洪姝屮轶趔后躔弪箦溴戾è痱轭舡汜箦轰秣钽狍濠í痱轭舡痱弭豉舂í疳汶徵濯ㄦ轭洵疳汶徵恒飑í屮痫螋螵┅痱轭Ж蹰镳轰彐轭瀛疳汶徵亥玟屮舢轭翦蜴徙ê躞恒飑秕舂翦蝠蜷秕舂戾è疳汶徵濯ㄦ轭洵疳汶徵吼顼滹舡痱轶糸铄┅痱轭Ж沆洪瞽疳汶徵亥玟屮舢轭翦蜴徙濠秕舂ㄥ痨镤瀛玟屮舡轭翦蜴徙秕轭翦蜴徙濠翦蝠蜷秕舂翦蝠蜷秕舂痱轭啜沆哄痫螋Ж括铗弼弪箦屮痫螋螵┅秕舂┅ㄤ彐躅珏铄蜥翦玟屮舡忾钿轭珞ī戾舄è蝻雉犏镱吼狎箦玟屮翦铙轱瞽轭翦蜴徙瀛骈戾┅ㄦ矧磲舡鲥蝮轱ㄧ弭栳箬㈡矧磲暨鲥蝮轱睥蝻雉┅躅戾篌骘蝽狒鲥蝮轱暴ㄥ蝌矧⒄铙躔痫螋邃鲥蝮轱骘蝽狒立骘蝽狒鲥蝮轱瞟ㄧ孱弪狒瀛玟屮舡豉疱忾钿轭珞ㄧ弭栳箬Ⅳ疱螈蝻雉┅ㄧ孱弪狒瀛玟屮舡轭翦蜴徙瀛忾钿轭珞ㄧ弭栳箬㈤铘弪驷沐蝻雉┅┅ㄤ彐躅蝈珏铄蜥翦忾钿轭珞īㄧ孱弪狒瀛玟屮舡忾钿轭珞鲠祯弩┅